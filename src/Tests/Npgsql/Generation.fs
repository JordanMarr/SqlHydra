module Npgsql.Generation

open Swensen.Unquote
open SqlHydra
open SqlHydra.Domain
open NUnit.Framework
open VerifyTests
open VerifyNUnit
open SqlHydra.Npgsql

let cfg = 
    {
        ConnectionString = DB.connectionString
        OutputFile = ""
        Namespace = "TestNS"
        IsCLIMutable = true
        IsMutableProperties = false
        NullablePropertyType = NullablePropertyType.Option
        ProviderDbTypeAttributes = true
        TableDeclarations = false
        Readers = Some { ReadersConfig.ReaderType = AppInfo.info.DefaultReaderType } 
        Filters = Filters.Empty
    }

let lazySchema = lazy NpgsqlSchemaProvider.getSchema cfg false

let getCode cfg =
    SchemaTemplate.generate cfg AppInfo.info lazySchema.Value "---" false

let inCode (str: string) cfg = 
    let code = getCode cfg
    code.Contains str =! true

let notInCode (str: string) cfg = 
    let code = getCode cfg
    code.Contains str =! false

[<Test; Ignore "Ignore">]
let ``Print Schema``() =
    let schema = NpgsqlSchemaProvider.getSchema cfg false
    printfn "Schema: %A" schema

[<Test; Ignore "Ignore">]
let ``Print Code``() =
    getCode cfg |> printfn "%s"

[<Test>]
let ``Verify Generated Code``() = task {
    let code = getCode cfg
            
    let settings = VerifySettings()
#if NET6_0
    settings.UseFileName("Generation.Verify Generated Code NET6")
#endif
#if NET8_0
    settings.UseFileName("Generation.Verify Generated Code NET8")
#endif
    settings.UseDirectory("./Verify")
    settings.ScrubLines(fun line -> line.StartsWith("// This code was generated by"))
    VerifierSettings.OmitContentFromException() // Cleans up FAKE build output
    let! _ = Verifier.Verify(code, settings)
    ()
}

[<Test>]
let ``Code Should Have Reader``() =
    cfg |> inCode "type HydraReader"

[<Test>]
let ``Code Should Not Have Reader``() =
    { cfg with Readers = None } |> notInCode "type HydraReader"

[<Test>]
let ``Code Should Have CLIMutable``() =
    { cfg with IsCLIMutable = true } |> inCode "[<CLIMutable>]"

[<Test>]
let ``Code Should Not Have CLIMutable``() =
    { cfg with IsCLIMutable = false } |> notInCode "[<CLIMutable>]"

[<Test>]
let ``Code Should Have Namespace``() =
    cfg |> inCode "namespace TestNS"

[<Test>]
let ``Should have Tables and PKs``() =
    let schema = lazySchema.Value
    
    let allColumns = 
        schema.Tables 
        |> List.collect (fun t -> t.Columns)

    let pks = allColumns |> List.filter (fun c -> c.IsPK)
    
    schema.Tables.Length >! 0

    let numberOfTables =
        schema.Tables
        |> Seq.filter (fun t -> t.Type = TableType.Table)
        |> Seq.length

    Assert.IsTrue(pks.Length > numberOfTables, "Expected at least one pk per table")
    Assert.IsTrue(pks.Length < allColumns.Length, "Every column should not be a PK")

[<Test>]
let ``Code Should Have ProviderDbTypeAttribute With Json``() =
    cfg |> inCode "[<ProviderDbType(\"Json\")>]"

[<Test>]
let ``Code Should Have ProviderDbTypeAttribute With Jsonb``() =
    cfg |> inCode "[<ProviderDbType(\"Jsonb\")>]"

